apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'androidx.navigation.safeargs.kotlin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

ext {
    compose_version = "1.0.5"
}

android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.kindnesswand"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME
        archivesBaseName = "kindnesswall-$versionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables.useSupportLibrary = true

        buildConfigField "Long", "READ_TIMEOUT_SECOND", READ_TIMEOUT_SECOND
        buildConfigField "Long", "CONNECTION_TIMEOUT_SECOND", CONNECTION_TIMEOUT_SECOND

        resValue "string", "google_maps_key", (project.findProperty("GOOGLE_MAPS_API_KEY") ?: "")
    }

    buildFeatures {
        dataBinding true
        viewBinding true
        compose true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            manifestPlaceholders = [isBranchSdkInTestMode: "true"]
        }
    }

    flavorDimensions "default"

    productFlavors {
        staging {
            versionNameSuffix 'Staging'
            buildConfigField "String", "URL_WEBAPI", "${STAGING_URL_WEBAPI_BASE_URL}"
        }

        production {
            buildConfigField "String", "URL_WEBAPI", "${PRODUCTION_URL_WEBAPI_BASE_URL}"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }


    composeOptions {
        kotlinCompilerExtensionVersion "$compose_version"
    }
}

dependencies {
    // Androidx Libs
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'com.google.android:flexbox:2.0.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    def roomVersion = '2.3.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"


    def koin_version = "2.0.1"
    implementation "org.koin:koin-android-viewmodel:$koin_version"

    def androidXAnnotations = '1.0.1'
    implementation "androidx.annotation:annotation:$androidXAnnotations"

    def coroutinesVersion = '1.5.2'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutinesVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutinesVersion"

    implementation "androidx.fragment:fragment-ktx:1.3.6"


    def navigation_version = "2.3.5"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-runtime-ktx:$navigation_version"

    def lifecycle_version = "2.3.1"
    implementation "androidx.lifecycle:lifecycle-process:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'
    //noinspection LifecycleAnnotationProcessorWithJava8
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    //KTX
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation "androidx.activity:activity-ktx:1.3.1"

    def retrofit_version = '2.6.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    def okhttp_version = '3.9.1'
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp_version"

    def glide_version = "4.12.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.skydoves:landscapist-glide:1.4.0"

    // firebase
    implementation 'com.google.firebase:firebase-messaging:20.2.0'
    implementation 'com.google.firebase:firebase-analytics:17.4.2'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0'

    def kotpref_version = "2.13.2"
    implementation "com.chibatching.kotpref:kotpref:$kotpref_version"
    implementation "com.chibatching.kotpref:gson-support:$kotpref_version"
    implementation "com.chibatching.kotpref:initializer:$kotpref_version"
    implementation "com.chibatching.kotpref:livedata-support:$kotpref_version"

    // zoomable image view
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'

    //recycler view indicator
    implementation 'com.ryanjeffreybrooks:indefinitepagerindicator:1.0.10'

    // chrome tab
    implementation "androidx.browser:browser:1.2.0"

    implementation "com.github.marlonlom:timeago:4.0.3"

    //recycler view swipe detector
    implementation 'it.xabaras.android:recyclerview-swipedecorator:1.2.2'

    implementation 'com.github.samanzamani.persiandate:PersianDate:0.8'

    //chucker debugger
    debugImplementation "com.github.chuckerteam.chucker:library:3.2.0"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:3.2.0"

    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation 'androidx.compose.material:material:1.0.5'
    implementation "com.google.android.material:compose-theme-adapter:1.0.5"

    implementation project(path: ':spotlight')

    implementation 'com.jakewharton.timber:timber:5.0.1'
}
